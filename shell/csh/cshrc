# List possibilities after an ambiguous completion.
set autolist
# Enable color display for the builtin ls-F and pass --color=auto to ls
set color
set complete = igncase
set prompt = "%{\e[32;1m%}`hostname -s` %{\e[35;1m%}%c%{\e[0m%} "

setenv GREP_OPTIONS "--color=auto"

# Setting the full path of our dotfiles directory automatically.
# This script can be executed through 3 different ways.
# - Executed directly by shell through symbolic link(e.g., ~/.cshrc -> cshrc)
# - sourced by other script through symbolic link(e.g., source ~/.cshrc)
# - sourced by other script directly(e.g., source cshrc)
# The tcsh and csh haven't equivalent of bash's ${BASH_SOURCE} or zsh's
# ${(%):-%x} from which the full path of the this script can be got, and the
# special environment variable `$_` can't help, because it isn't updated
# recursively, in another word, the command line to execute this script can't be
# got by `$_` when this script is executed by other script.
if (-dr /proc/$$/fd) then
    # Prefer to get the full path of this script through `/proc` file system.
    set SHRC = `ls -l /proc/$$/fd | grep -oE '/.*shell/csh/cshrc$'`
else
    # For the OS(e.g., Mac OS) that hasn't `/proc` file system, get it through
    # command `lsof`.
    set SHRC = `lsof -Ffn -wp $$ | grep -oE '/.*shell/csh/cshrc'`
endif
set OLD_DIR = `pwd`; cd `dirname ${SHRC}`/../..; unset SHRC
setenv DOTFILES `pwd`
cd ${OLD_DIR}; unset OLD_DIR

# Load platform independent alias and environment variable settings.
source ${DOTFILES}/shell/csh/alias_env.csh

# Put local runtime configurations in ~/.local.cshrc, they'll stay out of your
# main dotfiles repository (which maybe public on the internet, like this one).
# Anything can be done safely, because this is the last runtime configuration
# file current shell will load.
if (-f ~/.local.cshrc) then
    source ~/.local.cshrc
endif
